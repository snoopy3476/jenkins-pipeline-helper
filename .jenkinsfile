// Demo cycle JenkinsFile for Tmax CI/CD






/***************************************************
 *                                                 *
 *                [ Pod Template ]                 *
 *                                                 *
 ***************************************************/

podTemplateInfo = [
	label: 'jenkins-slave-pod', 
	containers: [
		containerTemplate(
			name: 'build-container',
			image: 'openjdk:11',
			command: 'cat',
			ttyEnabled: true,
		),
		containerTemplate(
			name: 'push-container',
			image: 'docker',
			command: 'cat',
			ttyEnabled: true
		),
	],
	volumes: [ 
		hostPathVolume(mountPath: '/var/run/docker.sock', hostPath: '/var/run/docker.sock'), 
	]
]





/***************************************************
 *                                                 *
 *                   [ Stages ]                    *
 *                                                 *
 ***************************************************/

pipelineStages = [



/***** Checkout Stage *****/

	'Checkout': {
		checkout scm
	},



/***** Build Stage *****/

	'Build': {
		container('build-container') {

			// parallel build
			parallel 'build-submodule-01' : {

				sh '''#!/bin/bash
					./gradlew build --stacktrace -x test
				'''

			}, 'build-submodule-02 (dummy)' : {

				sh '''#!/bin/bash
					./gradlew -v
					echo dummy build script 1
				'''

			}, 'build-submodule-03 (dummy)' : {

				sh '''#!/bin/bash
					./gradlew -v
					echo dummy build script 2
				'''

			}
		}
	},



/***** Test Stage *****/

	'Test': {
		container('build-container') {

			// parallel test
			parallel 'test-ApiTest' : {

				sh './gradlew test --stacktrace --tests="net.hwkim.apigw.ApiTest"'

			}, 'test-HelloSpringTest' : {

				sh './gradlew test --stacktrace --tests="net.hwkim.apigw.HelloSpringTest"'

			}
		}
	},



/***** Push Stage *****/

	'Push': {
		container('push-container') {
			echo '- Push'
			sleep 5
		}
	}

]





/***************************************************
 *                                                 *
 *              [ Stage Processing ]               *
 *                                                 *
 ***************************************************/

// run stage with some pre/post jobs for the stage
def runStage(stageName, stageCode) {
	stageRunning(stageName)
	stage(stageName) {
		try {
			stageCode()

		} catch (error) {
			stageFailed(stageName)
			throw error
		}			
	}
	stageSuccess(stageName)
}


// stage running processing
def stageRunning(stageName) {
	updateGitlabCommitStatus name: gitlabStageStrs[stageName], state: 'running'
}


// stage success processing
def stageSuccess(stageName) {
	updateGitlabCommitStatus name: gitlabStageStrs[stageName], state: 'success'
}


// stage fail processing
def stageFailed(stageName) {

	// notify cur stage as failed first
	updateGitlabCommitStatus name: gitlabStageStr(stageName), state: 'failed'

	// notify stages after the cur stage as canceled
	def stageToBeCanceled = false
	pipelineStages.each { key, value ->

		if (stageToBeCanceled) {
			updateGitlabCommitStatus name: gitlabStageStrs[key], state: 'canceled'
		} else if (key == stageName) {
			// stages after the cur stage will be notify as canceled
			stageToBeCanceled = true
		}
	}
}





/***************************************************
 *                                                 *
 *                [ Main Routine ]                 *
 *                                                 *
 ***************************************************/

def main() {

	// make array of gitlab stages (format: ##. stagename)
	gitlabStageStrs = ['podinit':'00. PodInit']
	pipelineStages.eachWithIndex { key, value, idx ->
		gitlabStageStrs.put(key, (idx+1).toString().padLeft(2, '0') + '. ' + key)
	}

	// notify gitlab pending stages
	gitlabBuilds(builds: gitlabStageStrs.values() as ArrayList) {


		// init pod, and iterate for defined stages
		stageRunning('podinit')
		def podinitsuccess = false
		try {

			
			podTemplate(podTemplateInfo) {


				node('jenkins-slave-pod') {
					// podinit finished
					stageSuccess('podinit')
					podinitsuccess = true



					pipelineStages.each{ key, value ->
						runStage(key, value)
					}


				}

			}

		} catch (error) {
			
			if (! podinitsuccess) {
				stageFailed('podinit')
			}
			throw error
		}
	}
}

main()
